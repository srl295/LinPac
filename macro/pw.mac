:macro pw
;;This macro provides the authorization to known station types
;;In station database the TYPE and PWD fields must be set
;;When using some older version of FlexNet that use the magic numbers
;;algorithm just don't set the PWD field and set the SYSNUM field
;;
;;Extended usage:
;;pw [command [system [password]]]
;;Sends specified command instead of the default one and uses specified
;;system instead of the one specified in the station database (STN_TYPE).

if %(exist STN_TYPE) == 1

  set FOUND 0

  if %R > 1
    set CMD "%1"
  else
    set CMD "sys"
  endif

  if %R > 2
    set TYPE "%2"
  else
    set TYPE "%STN_TYPE"
  endif
  
  ;;Older FlexNet nodes use 'magic' numbers for authorization
  if %(exist STN_SYSNUM) == 1

    ;;Old Flexnet password
    if "%TYPE" == "FLEXNET"
      flexpwd "%STN_SYSNUM"
      echo %CMD
      set FOUND 1
    endif

  else
  ;;Other algorithms use the password string
  if %(exist STN_PWD) == 1

    ;;Choose the password
    if %R > 3
      set PWD "%3"
    else
      set PWD "%STN_PWD"
    endif

    ;;New Flexnet password
    if "%TYPE" == "FLEXNET"
      :FLEXSTART
      set PROMPT "%<"     ;;clear the input line buffer
      set PROMPT ""
      echo %CMD            ;;send the request
      flush
      :FLEXLOOP             ;;wait for some prompt
      set PROMPT "%<"
      if ("%PROMPT" == "") goto FLEXLOOP
      tnpwd -n "%PWD" "%PROMPT" ;;calculate the password
      :FLEXLOOP1
      if (%(exist TNPWD) == 0) goto FLEXLOOP1
      if (%TNPWD == 0) goto FLEXSTART
      unset TNPWD
      set FOUND 1
      unset PROMPT
    endif

    ;;FPAC password
    if "%TYPE" == "FPAC"
      set STN_MD "5"
      goto FBBSTART ;; the same as FBB password
    endif

    ;;BAYBOX password
    if "%TYPE" == "BAYBOX"
      goto TNSTART ;; the same as TheNet password
    endif

    ;;FBB MD2/MD5 password
    if "%TYPE" == "FBB"
      :FBBSTART
      set PROMPT "%<"     ;;clear the input line buffer
      set PROMPT ""
      echo %CMD            ;;send the request
      flush
      :FBBLOOP            ;;wait for some prompt
      set PROMPT "%<"
      if ("%PROMPT" == "") goto FBBLOOP
      if "%STN_MD" == "5"
        md5pwd "%PWD" "%PROMPT" ;;calculate the password
      else
        md2pwd "%PWD" "%PROMPT" ;;calculate the password
      endif
      set FOUND 1
      unset PROMPT
    endif

    ;;TheNet password
    if "%TYPE" == "THENET"
      :TNSTART
      set PROMPT "%<"     ;;clear the input line buffer
      set PROMPT ""
      echo %CMD            ;;send the request
      flush
      :TNLOOP             ;;wait for some prompt
      set PROMPT "%<"
      if ("%PROMPT" == "") goto TNLOOP
      tnpwd "%PWD" "%PROMPT" ;;calculate the password
      :TNLOOP1
      if (%(exist TNPWD) == 0) goto TNLOOP1
      if (%TNPWD == 0) goto TNSTART
      unset TNPWD
      set FOUND 1
      unset PROMPT
    endif

    unset PWD
    
  else
    echo No password defined for %C
    set FOUND 1 ;;prevent two errors
  endif
  endif

  ;;Not found
  if %FOUND != 1
    echo Unknown station type %TYPE
  endif
  unset FOUND
  unset TYPE
  unset CMD

else
  echo Station type not defined for %C
endif
      