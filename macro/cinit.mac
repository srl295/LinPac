:macro cinit
;;
;; cinit.mac - this macro is called automatically when any connection ensures
;;
;; Parametres: %1 - callsign of connected station
;;             %2 - callsign of previously connected station
;;                  or empty when first connection
;;             %3 - contains either "connect" (connected forward)
;;                  or "reconnect" (reconnected back)
;;

rxflow off

;; Setup remote command acces list
if %(exist STN_RCMD) == 1
  rcmd %STN_RCMD
else
  rcmd %DEF_RCMD@0
endif

;; When a ENC variable is set for this station, change character translation
;; table
if %(exist STN_ENC) == 1
  translate %STN_ENC
else
  translate %DEF_ENC@0
endif

;; When a TERM variable is set, change terminal type
if %(exist STN_TERM) == 1
  term %STN_TERM
else
  term none
endif

;; When it's a primary connection 
if "%2" == ""
  ;;switch off the compression
  compress off
  ;;if a path to logbook is set, add a logbook  entry
  if %(exist LOG_PATH@0) == 1
    if %(exist STN_QRG) == 1
      log start %LOG_PATH@0/%Y %1 "%STN_QRG"
      set LOG_QRG "%STN_QRG"
    else
      if %(exist QRG@0) == 1
        log start %LOG_PATH@0/%Y %1 "%QRG@0"
        set LOG_QRG "%QRG@0"
      endif
    endif
  endif
endif

;; Reply to login password request (if any)
if "%3" == "connect"
  if %(exist STN_LOGINPW) == 1
    if %(exist STN_PWPROMPT) == 1
      ;; *** FBB VE2BLY C_FILTER ***
      if "%STN_TYPE" == "FBB"
        catch -i -m "Waiting for password prompt" "%STN_PWPROMPT*\r" ":tnpwd -n '%STN_LOGINPW' '%STN_PWPROMPT$1'"
      endif
      ;; *** BayBox system ***
      if "%STN_TYPE" == "BAYBOX"
        catch -i -m "Waiting for password prompt" "%STN_PWPROMPT*\r" ":tnpwd '%STN_LOGINPW' '%STN_PWPROMPT$1'"
      endif
      ;; *** (T)NOS MD5 (numeric) by SQ2FRB ***
      if "%STN_TYPE" == "TNOS"
        catch -i -m "Waiting for password prompt" "%STN_PWPROMPT*]" ":md5pwd -num '%STN_LOGINPW' '%STN_PWPROMPT$1]'"
      endif
      if "%STN_TYPE" == "NOS"
        catch -i -m "Waiting for password prompt" "%STN_PWPROMPT*]" ":md5pwd -num '%STN_LOGINPW' '%STN_PWPROMPT$1]'"
      endif
    else
      echo Cannot reply to password: no PWPROMPT set in station database
    endif
  endif
endif

rxflow on
